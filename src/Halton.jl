module Halton

# using Distributions
using Primes

export HaltonSeq!, HaltonDraws!

"""
    `HaltonSeq!{T<:AbstractFloat}(H::Vector{T}, B::Int; skip::Int=500)`

Replaces `H` with entries from Halton low discrepancy sequence with base `B`.
Elements in `H` take values in the interval (0, 1).
Keyword argument `skip` is the number initial "burn-in" elements to drop.
"""
function HaltonSeq!(H::AbstractArray{<:AbstractFloat}, B::Integer; skip::Integer=500, kwargs...)
  isprime(B) || error("base number not prime")
  H!(H, B, skip)
end

# """
#     `HaltonDraws!{T<:AbstractFloat}(H::Vector{T}, B::Int, [skip::Int=500, distr=Normal()])`

# Replaces `H` with draws from a distribution `Distributions.dist()`.
# Draws are generated by using Halton sequence with base `B` as the quantiles drawn.
# Keyword argument `skip` is the number initial "burn-in" elements to drop.
# """
# function HaltonDraws!(H::AbstractArray, B::Integer; skip::Integer=500, distr = Normal(), kwargs...)
#   HaltonSeq!(H, B, skip=skip)
#   H .= Distributions.quantile.(distr, H)
# end

####

## Algorithm for generating Halton sequences
function H!(H, base::Int, skip::Int)
  
  # Fill H with Halton Sequence based on b
  S = skip + length(H)
  
  # set D to get generated seq >= S
  D = ceil(Int, log(S) / log(base))
  
  # placeholders
  d = zeros(Int, D+1)
  r = zeros(Rational{Int}, D+1)

  # based on algorithm found in https://www.researchgate.net/publication/229173824_Fast_portable_and_reliable_algorithm_for_the_calculation_of_Halton_numbers
  for nn in 1:S
    l = 1
    
    while d[l] == base-1
      d[l] = 0
      l += 1
    end
    
    d[l] += 1
    
    if l >= 2
      r[l-1] = (d[l] + r[l]) // base
    end
    
    if l >= 3
      for j in (l-1) : -1 : 2
        r[j-1] = r[j] // base
      end
    end
    
    if nn>skip
      H[nn-skip] = (d[1] + r[1]) // base
    end
  
  end
  
  return H
end

end
